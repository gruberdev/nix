inputs: lib: _:
{
  autoloadedModules = let
    optionsDir = ../modules/autoload;
    nixFiles = lib.filterAttrs (n: _: lib.hasSuffix ".nix" n) (builtins.readDir optionsDir);
  in
    map (file: optionsDir + "/${file}") (builtins.attrNames nixFiles);

  imports = let
    modulePath = path:
      if builtins.isPath path
      then path
      else if builtins.substring 0 1 (toString path) == "/"
      then path
      else if builtins.pathExists ../modules/${path}
      then ../modules/${path}
      else ../modules/${path}.nix;
  in
    builtins.map modulePath;

  brew-alias = pkgs: name:
    lib.mkIf pkgs.stdenv.isDarwin
    (pkgs.stdenv.mkDerivation {
      name = "${name}-brew";
      version = "1.0.0";
      dontUnpack = true;
      installPhase = ''
        mkdir -p $out/bin
        ln -s /opt/homebrew/bin/${name} $out/bin/${name}
      '';
      meta = with pkgs.lib; {
        mainProgram = "${name}";
        description = "Wrapper for Homebrew-installed ${name}";
        platforms = platforms.darwin;
      };
    });
}
// inputs.home-manager.lib
// inputs.nix-darwin.lib
